title dodge
author misdake
homepage misdake.github.io
key_repeat_interval 0.3
verbose_logging
enable_level_select 1
(build with https://pancelor.com/PuzzleScript/editor.html)
 
========
OBJECTS
========

Temp
GRAY
.....
.....
.....
.....
.....

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Box
DARKBROWN BROWN
.000.
00110
01010
01100
.000.

Crate
BROWN DARKBROWN LIGHTBROWN
..00.
0111.
.1000
0.11.
.001.

DeadCrate
BROWN DARKBROWN LIGHTBROWN
..01.
0111.
.1000
0.11.
.001.

Target
DarkBlue
..0..
.000.
00.00
.000.
..0..

BallistaBG
DarkGreen DarkGray
00000
01000
00000
00010
00000
Hole
Green #004FFF #008FFF
01110
12111
11111
11211
01110
GlassQ
DarkGreen DarkGray Gray
.....
.2222
.2111
.2100
.2100
GlassE
DarkGreen DarkGray Gray
.....
2222.
1112.
0012.
0012.
GlassZ
DarkGreen DarkGray Gray
.2100
.2100
.2111
.2222
.....
GlassC
DarkGreen DarkGray Gray
0012.
0012.
1112.
2222.
.....
Glass1
DarkGreen DarkGray Gray
0012.
10122
00111
00000
01000
Glass2
DarkGreen DarkGray Gray
.....
22222
11111
00000
10000
Glass3
DarkGreen DarkGray Gray
.2100
22100
11101
00000
01000
Glass4
DarkGreen DarkGray Gray
0012.
0012.
0012.
0012.
1012.
Glass8
DarkGreen DarkGray Gray
10000
00000
11111
22222
.....
Glass6
DarkGreen DarkGray Gray
.2100
.2100
.2100
.2100
.2101
Glass7
DarkGreen DarkGray Gray
01000
00000
00111
10122
0012.
Glass9
DarkGreen DarkGray Gray
01000
00000
11101
22100
.2100

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

DeadPlayer
Black Orange White Blue Red brown
..5..
33510
.3410
33210
..2..

ArrowD
Red BROWN
.....
..1..
..1..
..1..
..0..
Ballista1D
White lightbrown GRAY BROWN
.222.
2.3.2
1.3.1
.131.
..0..
Ballista2D
White lightbrown GRAY
.....
22222
1...1
.111.
.....
Ballista3D
White lightbrown GRAY
.....
22222
1...1
.111.
.....
BallistaXD
gray darkgray darkgray Red BROWN
..00.
003.0
1.4.1
114.1
..4..

ArrowU
Red BROWN
..0..
..1..
..1..
..1..
.....
Ballista1U
White lightbrown GRAY BROWN
..0..
.131.
1.3.1
2.3.2
.222.
Ballista2U
White lightbrown GRAY
.....
.111.
1...1
22222
.....
Ballista3U
White lightbrown GRAY
.....
.111.
1...1
22222
.....
BallistaXU
gray darkgray darkgray Red BROWN
..4..
1.411
1.4.1
0.300
.00..

ArrowL
Red BROWN
.....
.....
0111.
.....
.....
Ballista1L
White lightbrown GRAY BROWN
..12.
.1..2
03332
.1..2
..12.
Ballista2L
White lightbrown GRAY
..12.
.1.2.
.1.2.
.1.2.
..12.
Ballista3L
White lightbrown GRAY
..12.
.1.2.
.1.2.
.1.2.
..12.
BallistaXL
gray darkgray darkgray Red BROWN
.110.
....0
44430
.1.0.
.110.

ArrowR
Red BROWN
.....
.....
.1110
.....
.....
Ballista1R
White lightbrown GRAY BROWN
.21..
2..1.
23330
2..1.
.21..
Ballista2R
White lightbrown GRAY
.21..
.2.1.
.2.1.
.2.1.
.21..
Ballista3R
White lightbrown GRAY
.21..
.2.1.
.2.1.
.2.1.
.21..
BallistaXR
gray darkgray darkgray Red BROWN
.011.
.01..
03444
0....
.011.

=======
LEGEND
=======

. = Background
# = Wall
B = Box
X = Crate
S = Crate and BallistaBG
Q = GlassQ
E = GlassE
Z = GlassZ
C = GlassC
1 = Glass1
2 = Glass2
3 = Glass3
4 = Glass4
6 = Glass6
7 = Glass7
8 = Glass8
9 = Glass9
O = Target
P = Player
~ = Hole
` = BallistaBG
D = Ballista1D and BallistaBG
U = Ballista1U and BallistaBG
L = Ballista1L and BallistaBG
R = Ballista1R and BallistaBG
Glass = Glass1 or Glass2 or Glass3 or Glass4 or Glass6 or Glass7 or Glass8 or Glass9 or GlassQ or GlassE or GlassZ or GlassC
BallistaX = BallistaXD or BallistaXU or BallistaXL or BallistaXR
Ballista1DU = Ballista1D or Ballista1U
Ballista1LR = Ballista1L or Ballista1R
BallistaD = Ballista1D or Ballista2D or Ballista3D
BallistaU = Ballista1U or Ballista2U or Ballista3U
BallistaL = Ballista1L or Ballista2L or Ballista3L
BallistaR = Ballista1R or Ballista2R or Ballista3R
BallistaDU = BallistaD or BallistaU
BallistaLR = BallistaL or BallistaR
Arrow = ArrowD or ArrowU or ArrowL or ArrowR

=======
SOUNDS
=======
Arrow Create 35956704
DeadPlayer Create 2856102
EndLevel 78313100

sfx0 94305102
BallistaX Create 94305102

sfx1 64520708

================
COLLISIONLAYERS
================

Background
BallistaBG
Target
Hole, Box, Player, DeadPlayer, Wall, Crate, DeadCrate, Glass, BallistaDU, BallistaLR, BallistaX
ArrowD
ArrowU
ArrowL
ArrowR
Temp

======
RULES
======

[DeadPlayer] -> restart

(reload iterations)
late [ Ballista3D ] -> [ Ballista1D ]
[ Ballista2D ] -> [ Ballista3D ]
late [ Ballista3U ] -> [ Ballista1U ]
[ Ballista2U ] -> [ Ballista3U ]
late [ Ballista3L ] -> [ Ballista1L ]
[ Ballista2L ] -> [ Ballista3L ]
late [ Ballista3R ] -> [ Ballista1R ]
[ Ballista2R ] -> [ Ballista3R ]


(ballista follow player)
[ horizontal Player | ... | Ballista1DU ] -> [ horizontal Player | ... | horizontal Ballista1DU ]
[ vertical Player | ... | Ballista1LR ] -> [ vertical Player | ... | vertical Ballista1LR ]

(ballista fire at player)
[ vertical Player | ... | Ballista1D ] -> [ vertical Player | ... | Ballista2D ArrowD ]
[ vertical Player | ... | Ballista1U ] -> [ vertical Player | ... | Ballista2U ArrowU ]
[ action Player | ... | Ballista1D ] -> [ action Player | ... | Ballista2D ArrowD ]
[ action Player | ... | Ballista1U ] -> [ action Player | ... | Ballista2U ArrowU ]
[ horizontal Player | ... | Ballista1L ] -> [ horizontal Player | ... | Ballista2L ArrowL ]
[ horizontal Player | ... | Ballista1R ] -> [ horizontal Player | ... | Ballista2R ArrowR ]
[ action Player | ... | Ballista1L ] -> [ action Player | ... | Ballista2L ArrowL ]
[ action Player | ... | Ballista1R ] -> [ action Player | ... | Ballista2R ArrowR ]


(arrow move)
[ ArrowD ] -> [ Down ArrowD ]
[ ArrowU ] -> [ Up ArrowU ]
[ ArrowL ] -> [ Left ArrowL ]
[ ArrowR ] -> [ Right ArrowR ]

(push box, arrow hit box)
[ Box Arrow ] -> [ Box ]
late [ Box Arrow ] -> [ Box ]
[> Player | Box ] -> [> Player | > Box]
[> Player | Box | Arrow ] -> [> Player | > Box | ]
[> Player | Box | Hole ] -> [> Player | | ] sfx1

(disable player moving into wall)
[> Player] -> [> Player Temp]
late [ Player Temp ] -> CANCEL
late [ Temp ] -> [ ]

(arrow hit Ballista -> die)
late [ BallistaD ArrowU ] -> [ BallistaXD ]
late [ BallistaU ArrowD ] -> [ BallistaXU ]
late [ BallistaL ArrowR ] -> [ BallistaXL ]
late [ BallistaR ArrowL ] -> [ BallistaXR ]

(arrow hit crate -> destroy)
late [ Arrow Crate ] -> [ DeadCrate ] sfx0
late [ Arrow DeadCrate ] -> [ DeadCrate ]
late [ DeadCrate ] -> []

(arrow hit wall -> disappear)
late [ Arrow Wall ] -> [ Wall ]
late [ Arrow BallistaBG ] -> [ BallistaBG ]

(arrow hit player -> die)
DOWN [ DOWN Player | ArrowU ] -> [ DeadPlayer | ]
UP [ UP Player | ArrowD ] -> [ DeadPlayer | ]
LEFT [ LEFT Player | ArrowR ] -> [ DeadPlayer | ]
RIGHT [ RIGHT Player | ArrowL ] -> [ DeadPlayer | ]
late [ Player Arrow ] -> [ DeadPlayer ]

==============
WINCONDITIONS
==============

all Player on Target
no DeadPlayer

=======
LEVELS
=======

(1. basic levels)

(1.1)
#######
#D````#
#88888#
#P....#
#.#.#.#
#.#.#.#
#.#.#.#
#....O#
#######

(1.2)
#######
#`D#D`#
#88#88#
#P.#.O#
#..#..#
#..#..#
##.#.##
##...##
#######

(1.3)
message press X to skip a turn

#######
#`D#D`#
#88888#
#....O#
#..#.##
##.#..#
##.#.##
#P...##
#######

(1.4)
#######
#P....#
#.#.#.#
#.#.#.#
#.#.#.#
#....O#
#22222#
#U````#
#######

(1.5)
#######
#D````#
#88888#
#.....#
#..#..#
#P..#O#
#22222#
#U`#U`#
#######

(2. crate)

(2.1)
#######
#P#...#
#.#.#.#
#....X#
#X#.#O#
#.#.#.#
#22222#
#U`U#U#
#######

(2.2)
#######
#`D#`D#
#88888#
#..X..#
##..#X#
#P.X#O#
#22222#
##UU``#
#######

(2.3)
#######
#D`D`D#
#88888#
#.#...#
#.#.#.#
#P..#O#
#22222#
##UU`U#
#######

(2.4)

#######
#D````#
#8889`#
#P..6L#
#.#.6`#
##..6L#
#..#6`#
#.XO6`#
#######

(2.5)
message press R to restart level

#######
#D`#``#
#88888#
#X.X..#
#X.X#X#
#PX.XO#
#22222#
##U``U#
#######

(3. more)

(3.1)
#######
#``D`D#
#`7888#
#R4X.P#
#`4.XX#
#R4...#
#`4##.#
#R4O..#
#######

(3.2)
#######
#D`D``#
#8889`#
#...6L#
#.X.6`#
#PXO6L#
#.X.6`#
#...6L#
#######

(3.3)
#######
#DD#``#
#8889`#
#..P6L#
#.#.6`#
#..X6L#
#XXX6`#
#.#O6`#
#######

(3.4)
#######
#D`D``#
#8889`#
#PXO6L#
#.#X6L#
#..X6L#
#.#X6`#
#...6`#
#######

(3.5)
#######
#```DD#
#`7888#
#R4P..#
#R4.X.#
##4XXX#
#R4X#X#
#R4OXX#
#######

(4. hole)

(4.1)
#######
#``D#D#
#88888#
#P~...#
#X..~.#
#~.~OX#
#22222#
#U#UUU#
#######

(4.2)
#######
#D`D``#
#8889`#
#X~P6`#
#...6L#
#.X~6L#
#.XX6L#
#.#O6`#
#######

(4.3)
#######
#DD#``#
#8889`#
#O..6`#
#X~X6`#
#.X.6L#
#~.X6L#
#P..6L#
#######

(4.4)
#######
#DDD``#
#8889`#
#...6`#
#.~~6`#
#...6L#
#~..6L#
#P..OL#
#######

(4.5)
#``D###
#`79`D#
#`4Z88#
#R4O.X#
#`4~~.#
#R4.X.#
#R4X.##
#R4.P.#
#######

(5. box)

(5.1)
#######
#D``#D#
#88888#
#.#.B.#
#.#.#O#
#...#.#
#B#.#.#
#P#...#
#######

(5.2)
#######
#DDDDD#
#88888#
#...#O#
#.#.B.#
#.#BBX#
#B.B..#
#PB...#
#######

(5.3)
#######
#DDD`D#
#88888#
#...#O#
#.X.#~#
#~XB.B#
#B..#.#
#P.X..#
#######

(5.4)
#DD``##
#88888#
#...~~#
#.B.#.#
#.X.#.#
#.B.#O#
#P.XB.#
#22222#
#U``#U#

(5.5)
#`D#`D#
#88888#
#P.X..#
#..BBX#
#.B.B~#
#B.X~~#
#.B~~O#
#22222#
#UUUUU#

(6. final levels)

(6.1)
####D##
``D789`
`78C.6`
R4O.X6L
#4.X.6`
R4.X.6`
`4P#.6`
`4..X6L
#######

(6.2)
#`D#D``
#88889`
#....6L
#.~#O6L
#..B.6L
#..#~6L
#XB..6L
#..P.6L
#######

(6.3)
#######
#O#DD``
#X#889`
#XXX.6L
#.#..6L
#XX.p6L
#22223`
#```U``
#######

(6.4)
#`DD#``
#88889`
#.X..6L
#.BP.6L
#.B.~6L
#XB#~6`
#...O6L
#22223`
#UU#U``

(6.5)
``DD```
`78889`
R4P..6L
R4..~6L
R4...6L
#4.~.6#
`4..O6L
`12223`
``UUU``