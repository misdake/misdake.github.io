title dodge
author misdake
homepage misdake.github.io
key_repeat_interval 0.3
verbose_logging
========
OBJECTS
========

Temp
GRAY
.....
.....
.....
.....
.....

Background
LIGHTGREEN GREEN
11111
01111
11101
11111
10111

Wall
BROWN DARKBROWN
00010
11111
01000
11111
00010

Target
DarkBlue
..0..
.000.
00.00
.000.
..0..

Glass1
GRAY lightgray
.....
00.00
11.11
00.00
.....

Player
Black Orange White Blue
.000.
.111.
22222
.333.
.3.3.

DeadPlayer
Black Orange White Blue Red brown
..5..
33510
.3410
33210
..2..

ArrowDn
Red BROWN
.....
..1..
..1..
..1..
..0..

Ballista1Dn
White lightbrown GRAY BROWN
.222.
2.3.2
1.3.1
.131.
..0..

Ballista2Dn
White lightbrown GRAY
.....
22222
1...1
.111.
.....
Ballista3Dn
White lightbrown GRAY
.....
22222
1...1
.111.
.....
BallistaXDn
gray darkgray darkgray Red BROWN
..00.
003.0
1.4.1
114.1
..4..

ArrowUp
Red BROWN
..0..
..1..
..1..
..1..
.....

Ballista1Up
White lightbrown GRAY BROWN
..0..
.131.
1.3.1
2.3.2
.222.

Ballista2Up
White lightbrown GRAY
.....
.111.
1...1
22222
.....
Ballista3Up
White lightbrown GRAY
.....
.111.
1...1
22222
.....
BallistaXUp
gray darkgray darkgray Red BROWN
..4..
1.411
1.4.1
0.300
.00..


=======
LEGEND
=======

. = Background
# = Wall
- = Glass1
O = Target
P = Player
D = Ballista1Dn
U = Ballista1Up
BallistaX = BallistaXDn or BallistaXUp
Ballista1 = Ballista1Dn or Ballista1Up
BallistaDn = Ballista1Dn or Ballista2Dn or Ballista3Dn
BallistaUp = Ballista1Up or Ballista2Up or Ballista3Up
Ballista = BallistaDn or BallistaUp
Arrow = ArrowDn or ArrowUp

=======
SOUNDS
=======
Arrow Create 35956704
DeadPlayer Create 2856102
BallistaX Create 31649102
EndLevel 78313100

================
COLLISIONLAYERS
================

Background
Target
Player, DeadPlayer, Wall, Glass1, BallistaDn, BallistaUp, BallistaX
ArrowDn
ArrowUp
Temp

======
RULES
======

[DeadPlayer] -> restart

(reload iterations)
late [ Ballista3Dn ] -> [ Ballista1Dn ]
[ Ballista2Dn ] -> [ Ballista3Dn ]
late [ Ballista3Up ] -> [ Ballista1Up ]
[ Ballista2Up ] -> [ Ballista3Up ]


(ballista follow player)
[ horizontal Player | ... | Ballista1 ] -> [ horizontal Player | ... | horizontal Ballista1 ]

(ballista fire at player)
[ vertical Player | ... | Ballista1Dn ] -> [ vertical Player | ... | Ballista2Dn ArrowDn ]
[ vertical Player | ... | Ballista1Up ] -> [ vertical Player | ... | Ballista2Up ArrowUp ]
[ action Player | ... | Ballista1Dn ] -> [ action Player | ... | Ballista2Dn ArrowDn ]
[ action Player | ... | Ballista1Up ] -> [ action Player | ... | Ballista2Up ArrowUp ]


(arrow move)
[ ArrowDn ] -> [ Down ArrowDn ]
[ ArrowUp ] -> [ Up ArrowUp ]


(disable player moving into wall)
[> Player] -> [> Player Temp]
late [ Player Temp ] -> CANCEL
late [ Temp ] -> [ ]

(arrow hit wall -> disappear)
late [ Arrow Wall ] -> [ Wall ]
(arrow hit player -> die)
late [ Player Arrow ] -> [ DeadPlayer ]

DOWN [ BallistaDn | ArrowUp ] -> [ stationary BallistaXDn | ]
UP [ BallistaUp | ArrowDn ] -> [ stationary BallistaXUp | ]

==============
WINCONDITIONS
==============

all Player on Target
no DeadPlayer

=======
LEVELS
=======

#######
#D....#
#-----#
#P....#
#.#.#.#
#.#.#.#
#.#.#.#
#....O#
#######

#######
#.D#D.#
#--#--#
#P.#.O#
#..#..#
#..#..#
##.#.##
##...##
#######

message press X to stay still

#######
#.D#D.#
#-----#
#....O#
#..#.##
##.#..#
##.#.##
#P...##
#######

#######
#P....#
#.#.#.#
#.#.#.#
#.#.#.#
#....O#
#-----#
#U....#
#######

#######
#D....#
#-----#
#.....#
#..#..#
#P..#O#
#-----#
#U.#U.#
#######

#######
#D.D..#
#-----#
#.#...#
#.#.#.#
#P..#O#
#-----#
#UUU..#
#######
